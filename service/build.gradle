import com.squareup.wire.WireCompiler
import org.junit.platform.console.options.Details

apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'com.github.johnrengelman.shadow'

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

sourceSets {
  main.java.srcDirs += 'src/main/kotlin/'
  test.java.srcDirs += 'src/test/kotlin/'
}

junitPlatform {
  details Details.VERBOSE
}

dependencies {
  compile dep.guava
  compile dep.guice
  compile dep.guiceMultibindings
  compile dep.jacksonDatabind
  compile dep.jacksonDataformatYaml
  compile dep.jacksonJsr310
  compile dep.jacksonKotlin
  compile dep.jCommander
  compile dep.jettyServer
  compile dep.jettyServlet
  compile dep.jettyWebsocketServlet
  compile dep.jettyWebsocketServer
  compile dep.kotlinStdLib
  compile dep.kotlinReflection
  compile dep.loggingApi
  compile dep.metricsCore
  compile dep.metricsParent
  compile dep.moshi
  compile dep.okHttp
  compile dep.okio
  compile dep.openTracing
  compile dep.openTracingOkHttp
  compile dep.tracingJaeger
  compile dep.wireRuntime
  compile dep.wireCompiler
  compile dep.wireSchema
  compile dep.misk
  compile dep.miskHibernate
  compile dep.miskZipkin
  compile dep.skim
  compile project(':client')

  compile(dep.misk) {
    exclude group: 'com.google.guava'
  }
  testCompile(dep.miskTesting) {
    exclude group: 'com.google.guava', module: 'guava'
  }
  compile(dep.skim) {
    exclude group: 'com.google.guava'
  }

  testCompile dep.miskTesting
  testCompile dep.miskHibernateTesting
  testCompile dep.junitApi
  testCompile dep.junitParams
  testCompile dep.junitEngine
  testCompile dep.assertj
  testCompile dep.mockito
  testCompile dep.mockitoKotlin
  testCompile dep.openTracingMock
}

jar {
  manifest {
    attributes 'Main-Class': 'com.squareup.backfila.service.BackfilaServiceKt'
  }
  zip64 = true
  classifier = 'unshaded'
}

shadowJar {
  exclude('module-info.class') // https://github.com/johnrengelman/shadow/issues/352
  mergeServiceFiles()
  zip64 true
  classifier = null
}

task compileWire {
  doLast {
    //TODO(drashkov): upgrade wire compiler with a flag to create output directory
    def base = rootProject.findProject(":client").file("build/generated/source/proto/main/java")
    println "Creating $base/com/squareup/protos/cash/backfila"
    "mkdir -p $base/com/squareup/protos/cash/backfila".execute()

    def args = [
            "--proto_path=" + project.file("src/main/proto"),
            "--java_out=" + base,
            "squareup/backfila/service.proto",
            "squareup/backfila/client_service.proto",
    ]
    WireCompiler.main(args.toArray(new String[args.size()]))
  }
}

compileKotlin.dependsOn compileWire
